apply plugin: 'com.android.application'

ext {
    version_sdk = [
            compileSdk: 27,
            minSdk    : 14,
    ]
    version_lib = [
            appcompat: "27.0.0",
            butterknife: "8.8.1",
            commonmark: "0.10.0",
    ]
}

android {
    compileSdkVersion version_sdk.compileSdk
    flavorDimensions "default"

    defaultConfig {
        minSdkVersion version_sdk.minSdk
        targetSdkVersion version_sdk.compileSdk
        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField("String[]", "APPLICATION_LANGUAGES", '{' + getUsedAndroidLanguages().collect {
            "\"${it}\""
        }.join(",") + '}')
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""

        applicationId "net.gsantner.markor"
        versionName "0.1.6"
        versionCode 7
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavorDefault {
        }
        flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }
        flavorTest {
            applicationId "net.gsantner.markor_test"
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Android standard libs
    implementation "com.android.support:appcompat-v7:${version_lib.appcompat}"
    implementation "com.android.support:design:${version_lib.appcompat}"
    implementation "com.android.support:support-v4:${version_lib.appcompat}"
    implementation "com.android.support:recyclerview-v7:${version_lib.appcompat}"
    implementation "com.android.support:palette-v7:${version_lib.appcompat}"

    // UI libs
    implementation 'com.mobsandgeeks:adapter-kit:0.5.3'
    implementation "com.atlassian.commonmark:commonmark:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-gfm-strikethrough:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-gfm-tables:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-autolink:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-ins:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-yaml-front-matter:${version_lib.commonmark}"
    implementation "com.atlassian.commonmark:commonmark-ext-heading-anchor:${version_lib.commonmark}"

    implementation 'com.pixplicity.generate:library:1.1.8'

    // Tool libraries
    implementation "com.jakewharton:butterknife:${version_lib.butterknife}"
    implementation 'commons-io:commons-io:2.5'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.8.0'

    // Processors
    annotationProcessor "com.jakewharton:butterknife-compiler:${version_lib.butterknife}"
}
