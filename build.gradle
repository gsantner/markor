/*#######################################################
 *
 * SPDX-FileCopyrightText: 2017-2024 Gregor Santner <gsantner AT mailbox DOT org>
 * SPDX-License-Identifier: Unlicense OR CC0-1.0
 *
#########################################################*/
// Top-level build file where you can add configuration options common to all sub-projects/modules.

import java.text.SimpleDateFormat

buildscript {
    ext {
        version_gradle_tools = '8.5.1'
        version_plugin_kotlin = "1.3.72"
        enable_plugin_kotlin = false

        version_compileSdk = 35
        version_buildTools = "35.0.0"
        version_minSdk = 17
    }

    repositories {
        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${version_gradle_tools}"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        if (project.enable_plugin_kotlin) {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${version_plugin_kotlin}"
        }

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url "https://jitpack.io" }
        jcenter()
        google()
    }

    tasks.matching { task -> task.name.matches('(.*generate.*Resources)|(.*map.*SourceSetPaths)') }.all {
        task -> task.dependsOn copyRepoFiles
    }

    tasks.matching { it instanceof Test }.all { // Enable unit test output, html+xml output
        testLogging.events "passed", "skipped", "failed", "standardOut", "standardError"
        testLogging.showStandardStreams = true
        reports{
            junitXml { enabled true }
            html { enabled true }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

final String[] ROOT_TO_RAW_COPYFILES = ["README.md", "CHANGELOG.md", "CONTRIBUTORS.md", "LICENSE.txt", "LICENSE.md", "LICENSE"]
task copyRepoFiles(type: Copy) {
    from rootProject.files(ROOT_TO_RAW_COPYFILES)
    into "app/src/main/res/raw"
    rename { String fileName -> fileName.replace(fileName, fileName.toLowerCase()) }
}


@SuppressWarnings(["UnnecessaryQualifiedReference", "SpellCheckingInspection", "GroovyUnusedDeclaration"])
// Returns used android languages as a buildConfig array: {'de', 'it', ..}"
static String findUsedAndroidLocales() {
    Set<String> langs = new HashSet<>()
    new File('.').eachFileRecurse(groovy.io.FileType.DIRECTORIES) {
        final foldername = it.name
        if (foldername.contains('values-') && !it.absolutePath.contains("build" + File.separator + "intermediates") && !foldername.matches(".*values-((.*[0-9])|(land)|(port)).*")) {
            new File(it.toString()).eachFileRecurse(groovy.io.FileType.FILES) {
                if (it.exists() && it.name.toLowerCase().endsWith(".xml") && it.getText('UTF-8').contains("<string")) {
                    langs.add(foldername.replace("values-", ""))
                }
            }
        }
    }
    return '{' + langs.sort().collect { "\"${it}\"" }.join(",") + '}'
}



ext.getGitHash = providers.exec {
    commandLine('git', 'rev-parse', 'HEAD')
}.standardOutput.asText.get().trim()

ext.getGitLastCommitMessage = providers.exec {
    commandLine('git', 'log', '--oneline', '-1', '--format="%s"')
}.standardOutput.asText.get().replace("\"", "").replace("\\", "").trim()

@SuppressWarnings(["UnnecessaryQualifiedReference", "SpellCheckingInspection", "GroovyUnusedDeclaration"])
// Returns the build date in a RFC3339 compatible format. TZ is always converted to UTC
static String getBuildDate() {
    final SimpleDateFormat RFC3339_LIKE = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    RFC3339_LIKE.setTimeZone(TimeZone.getTimeZone("UTC"))
    return RFC3339_LIKE.format(new Date())
}
